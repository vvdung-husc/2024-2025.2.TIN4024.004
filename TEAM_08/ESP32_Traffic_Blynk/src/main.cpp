
//L√™ VƒÉn B√πi
#define BLYNK_TEMPLATE_ID "TMPL6EcyWZBb6"
#define BLYNK_TEMPLATE_NAME "Bui Le"
#define BLYNK_AUTH_TOKEN "waDCU5hEJF7fR0uJRETW6RcF-GCudH0s"

/* L√™ Quang V≈©
#define BLYNK_TEMPLATE_ID "TMPL6-MCfoVVn"
#define BLYNK_TEMPLATE_NAME "ESP32 Traffic Blynk"
#define BLYNK_AUTH_TOKEN "cKdXp_mMqaZvmoGmAiMqRoSjqge9ZIDR"
*/

/*
//Nguy·ªÖn ƒê·ª©c Nhu·∫≠n
#define BLYNK_TEMPLATE_ID "TMPL6R6i378Rq"
#define BLYNK_TEMPLATE_NAME "Traffic and DHT BanDemDoiMau"
#define BLYNK_AUTH_TOKEN "opLgjE7q8mUhNx5cFyasCUQTzuKBM3vW"
*/

//L√™ H·ªØu ƒê√¥ng Qu√¢n
// #define BLYNK_TEMPLATE_ID "TMPL61-yqowgg"
// #define BLYNK_TEMPLATE_NAME "ESMART"
// #define BLYNK_AUTH_TOKEN "WXoOHSnkx8oZOHg_910tFxUleUMuhWz1"

//ƒê·∫∑ng Ng·ªçc Ho√†ng
// #define BLYNK_TEMPLATE_ID "TMPL6pL3Elp5X"
// #define BLYNK_TEMPLATE_NAME "ESMART"
// #define BLYNK_AUTH_TOKEN "LbculCQ3x3GFQe0UxCNHFvh2-DiKI_wK"


#include <Arduino.h>
#include <TM1637Display.h>
#include <WiFi.h>
#include <WiFiClient.h>
#include <BlynkSimpleEsp32.h>
#include "DHT.h"

//Pin
#define rLED  27
#define yLED  26
#define gLED  25

// C·∫£m bi·∫øn √°nh s√°ng (LDR)
#define LDR_PIN 32
int ldrThreshold = 1000;  // M·∫∑c ƒë·ªãnh 1000, c√≥ th·ªÉ thay ƒë·ªïi t·ª´ Blynk


// Th·ªùi gian ƒë√®n
//1000 ms = 1 seconds
uint rTIME = 5000;   //5 seconds
uint yTIME = 3000;
uint gTIME = 7000;
uint blinkInterval = 500; // Th·ªùi gian nh·∫•p nh√°y

char ssid[] = "Wokwi-GUEST";
char pass[] = "";

#define btnBLED  23
#define pinBLED  21
#define CLK 18
#define DIO 19
#define DHTPIN 16
#define DHTTYPE DHT22

ulong currentMiliseconds = 0;
ulong ledTimeStart = 0;
ulong nextTimeTotal = 0;
ulong blinkStartTime = 0;
bool isBlinking = false;
bool yellowState = false;
bool blueButtonON = true;

int currentLED = rLED;
uint remainingTime = 0;

TM1637Display display(CLK, DIO);
DHT dht(DHTPIN, DHTTYPE);

bool IsReady(ulong &ulTimer, uint32_t milisecond);
void NonBlocking_Traffic_Light();
void NonBlocking_Traffic_Light_TM1637();
void Check_Light_Sensor();
void Blink_Yellow_Light();
void updateBlueButton();
void uptimeBlynk();
void updateDHT();

void setup() {
  Serial.begin(115200);
  pinMode(pinBLED, OUTPUT);
  pinMode(btnBLED, INPUT_PULLUP);
  pinMode(rLED, OUTPUT);
  pinMode(yLED, OUTPUT);
  pinMode(gLED, OUTPUT);
  pinMode(LDR_PIN, INPUT);
  
  display.setBrightness(0x0f);
  dht.begin();

  digitalWrite(yLED, LOW);
  digitalWrite(gLED, LOW);
  digitalWrite(rLED, HIGH);

  currentLED = rLED;
  nextTimeTotal += rTIME;
  remainingTime = rTIME / 1000;
  
  Serial.print("Connecting to "); Serial.println(ssid);
  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, pass);

  Serial.println("WiFi connected");
  digitalWrite(pinBLED, blueButtonON ? HIGH : LOW);
  Blynk.virtualWrite(V1, blueButtonON);
  
  Serial.println("== START ==>");
  Serial.print("1. RED    => GREEN  "); Serial.print(nextTimeTotal/1000); Serial.println(" (ms)");
}

void loop() {  
  Blynk.run();
  currentMiliseconds = millis();
  Check_Light_Sensor();
  uptimeBlynk();
  updateBlueButton();
  updateDHT();

  if (isBlinking) {
    Blink_Yellow_Light();
  } else {
    NonBlocking_Traffic_Light();
    NonBlocking_Traffic_Light_TM1637();
  }
}

bool IsReady(ulong &ulTimer, uint32_t milisecond) {
  if (currentMiliseconds - ulTimer < milisecond) return false;
  ulTimer = currentMiliseconds;
  return true;
}

void NonBlocking_Traffic_Light(){
  switch (currentLED) {
    case rLED: // ƒê√®n ƒë·ªè: 5 gi√¢y
      if (IsReady(ledTimeStart, rTIME)) {
        digitalWrite(rLED, LOW);
        digitalWrite(gLED, HIGH);
        currentLED = gLED;
        nextTimeTotal += gTIME;
        Serial.print("2. GREEN  => YELLOW "); Serial.print(nextTimeTotal/1000); Serial.println(" (ms)");
      } 
      break;

    case gLED: // ƒê√®n xanh: 7 gi√¢y
      if (IsReady(ledTimeStart,gTIME)) {        
        digitalWrite(gLED, LOW);
        digitalWrite(yLED, HIGH);
        currentLED = yLED;
        nextTimeTotal += yTIME;
        Serial.print("3. YELLOW => RED    "); Serial.print(nextTimeTotal/1000); Serial.println(" (ms)");        
      }
      break;

    case yLED: // ƒê√®n v√†ng: 2 gi√¢y
      if (IsReady(ledTimeStart,yTIME)) {        
        digitalWrite(yLED, LOW);
        digitalWrite(rLED, HIGH);
        currentLED = rLED;
        nextTimeTotal += rTIME;
        Serial.print("1. RED    => GREEN  "); Serial.print(nextTimeTotal/1000); Serial.println(" (ms)");        
      }
      break;
  }  
}

void NonBlocking_Traffic_Light_TM1637(){
  if (!isBlinking && blueButtonON) {  // Ch·ªâ hi·ªÉn th·ªã s·ªë khi n√∫t xanh b·∫≠t
    remainingTime = (ledTimeStart + 
                    (currentLED == rLED ? rTIME : 
                     (currentLED == gLED ? gTIME : yTIME))
                    - currentMiliseconds) / 1000;

    display.showNumberDec(remainingTime, true);
  } else {
    display.clear();  // N·∫øu Blue LED t·∫Øt, x√≥a s·ªë
  }
}

void Check_Light_Sensor() {
  int lightValue = analogRead(LDR_PIN);

  Blynk.virtualWrite(V4, ldrThreshold);  // G·ª≠i gi√° tr·ªã c·∫£m bi·∫øn l√™n Blynk
  
  if (lightValue < ldrThreshold) {  // S·ª≠ d·ª•ng bi·∫øn c√≥ th·ªÉ thay ƒë·ªïi
      if (!isBlinking) {
          isBlinking = true;
          blinkStartTime = millis();
          digitalWrite(rLED, LOW);
          digitalWrite(gLED, LOW);
          digitalWrite(yLED, LOW);
          display.clear(); // Xo√° s·ªë hi·ªÉn th·ªã khi v√†o ch·∫ø ƒë·ªô nh·∫•p nh√°y
          Serial.println("Low light detected - Blinking Yellow Mode");
      }
  } else {
      if (isBlinking) {
          isBlinking = false;
          digitalWrite(yLED, LOW);
          currentLED = rLED;
          ledTimeStart = millis();
          remainingTime = rTIME / 1000;
          display.showNumberDec(remainingTime);
          Serial.println("Light restored - Returning to normal mode");
      }
  }
}

// H√†m n√†y nh·∫≠n gi√° tr·ªã m·ªõi t·ª´ Blynk ƒë·ªÉ c·∫≠p nh·∫≠t ng∆∞·ª°ng s√°ng
BLYNK_WRITE(V4) {
  ldrThreshold = param.asInt(); // Nh·∫≠n gi√° tr·ªã ng∆∞·ª°ng t·ª´ Blynk
  Serial.print("üîÑ Ng∆∞·ª°ng √°nh s√°ng m·ªõi: ");
  Serial.println(ldrThreshold);
}


void Blink_Yellow_Light() {
  if (IsReady(blinkStartTime, blinkInterval)) {
    yellowState = !yellowState;
    digitalWrite(yLED, yellowState);
  }
  display.clear();
}

void updateBlueButton() {
  static ulong lastTime = 0;
  static int lastValue = HIGH;
  if (!IsReady(lastTime, 50)) return;  
  int v = digitalRead(btnBLED);
  if (v == lastValue) return;  
  lastValue = v;
  if (v == LOW) return;

  if (!blueButtonON) {  // N·∫øu b·∫£ng LED ƒëang t·∫Øt -> B·∫¨T
    Serial.println("Blue Light ON");
    digitalWrite(pinBLED, HIGH);
    blueButtonON = true;
    Blynk.virtualWrite(V1, blueButtonON);
  } else {  // N·∫øu b·∫£ng LED ƒëang b·∫≠t -> T·∫ÆT
    Serial.println("Blue Light OFF");
    digitalWrite(pinBLED, LOW);    
    blueButtonON = false;
    Blynk.virtualWrite(V1, blueButtonON);
  }  
}



void uptimeBlynk() {
  static ulong lastTime = 0;
  if (!IsReady(lastTime, 1000)) return;
  ulong value = lastTime / 1000;
  Blynk.virtualWrite(V0, value);
  if (!isBlinking && currentLED == 0) { 
    display.showNumberDec(value);
  } 
}

void updateDHT() {
  static ulong lastTime = 0;
  if (!IsReady(lastTime, 2000)) return;
  
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();
  
  if (isnan(temperature) || isnan(humidity)) {
    Serial.println("‚ö†Ô∏è Failed to read from DHT sensor! Retrying...");
    return;
  }

  // G·ª≠i d·ªØ li·ªáu l√™n Blynk
  Blynk.virtualWrite(V2, temperature);
  Blynk.virtualWrite(V3, humidity);
}



BLYNK_WRITE(V1) {
  blueButtonON = param.asInt();
  if (blueButtonON) {
    Serial.println("Blynk -> Blue Light ON");
    digitalWrite(pinBLED, HIGH);
    
    // HI·ªÇN TH·ªä L·∫†I S·ªê GI√ÇY C·ª¶A ƒê√àN GIAO TH√îNG
    if (!isBlinking) {  
      display.showNumberDec(remainingTime);
    }

  } else {
    Serial.println("Blynk -> Blue Light OFF");
    digitalWrite(pinBLED, LOW);   
    display.clear();  // T·∫ÆT B·∫¢NG LED
  }
}
